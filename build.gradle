buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            name = 'forge'
            url = 'http://files.minecraftforge.net/maven'
        }
        maven {
            name = 'sonatype-snapshots'
            url = 'https://oss.sonatype.org/content/repositories/snapshots'
        }
        dependencies {
            classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
        }
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'

version = 'MC1.9-INDEV'
group = 'darkevilmac.archimedesshipsplus'
archivesBaseName = 'archimedesshipsplus'

minecraft {
    version = "1.9-12.16.0.1859-1.9"
    runDir = "run"
    mappings = "snapshot_20160401"

    replace '@AS+VER@', project.version
}

dependencies {
    compile project(':MovingWorld')
}

processResources
        {
            inputs.property 'version', project.version
            inputs.property 'mcversion', project.minecraft.version

            from(sourceSets.main.resources.srcDirs) {
                include 'mcmod.info'

                expand 'version': project.version, 'mcversion': project.minecraft.version
            }

            from(sourceSets.main.resources.srcDirs) {
                exclude 'mcmod.info'
            }
        }


repositories {
    mavenCentral()
    maven {
        url 'http://repo.spongepowered.org/maven'
    }
    maven {
        url 'https://oss.sonatype.org/content/groups/public'
    }
}
idea {
    workspace {
        iws.withXml { xmlFile ->
            //Please don't kill me for my shitty groovy usage ;-;
            def runManager = xmlFile.asNode().component.find { it.@name == 'RunManager' }

            def minecraftClientDefaults = runManager.configuration.find {
                it.@name == 'Minecraft Client' && it.@type == 'Application'
            }

            def minecraftServerDefaults = runManager.configuration.find {
                it.@name == 'Minecraft Server' && it.@type == 'Application'
            }

            def minecraftClientVMArgs = '-Xincgc -Xmx1024M -Xms1024M -Dfml.coreMods.load=darkevilmac.movingworld.common.asm.coremod.MovingWorldCoreMod -Dmixin.debug=true'
            minecraftClientDefaults.option.find { it.@name == 'VM_PARAMETERS' }.replaceNode {
                option(name: 'VM_PARAMETERS', value: minecraftClientVMArgs)
            }

            def minecraftServerVMArgs = '-Xincgc -Dfml.ignoreInvalidMinecraftCertificates=true -Dfml.coreMods.load=darkevilmac.movingworld.common.asm.coremod.MovingWorldCoreMod -Dmixin.debug=true'
            minecraftServerDefaults.option.find { it.@name == 'VM_PARAMETERS' }.replaceNode {
                option(name: 'VM_PARAMETERS', value: minecraftServerVMArgs)
            }
        }
    }
}
